{
  "cells": [
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "# Case Study: Orbital Velocities\n",
        "\n",
        "You may have learned that the velocity of an idealized circular orbit --- a \n",
        "massless particle moving in the Newtonian gravitational field of a single \n",
        "point-mass --- is related to the radius of the orbit: to increase your orbital \n",
        "radius, increase your speed. More formally, the speed of an massless particle in \n",
        "this same idealized circular orbit can be found by taking the square root of \n",
        "the mass parameter divided by orbital radius. The planets in our solar system \n",
        "have near-circular orbits. We can compare their speeds, and recover the \n",
        "relationship that orbital speed has with orbital radius. \n",
        "\n",
        "$$V = \\sqrt{\\frac{\\mu}{r}}$$\n",
        "\n",
        "First, let's load the proper ephemeris data using `SPICEKernels`,\n",
        "`SPICEBodies`, and `SPICE`. We will load the planet's _barycenters_ here:\n",
        "the center of mass of their orbital system (the planet, and its moons).\n",
        "Just for fun, let's include Pluto and see what happens!"
      ]
    },
    {
      "cell_type": "code",
      "metadata": {},
      "source": [
        "using SPICE, SPICEKernels, SPICEBodies\n",
        "\n",
        "furnsh(\n",
        "    de440s(),                   # position and velocity data for inner planet    \n",
        "    latest_leapseconds_lsk(),   # timekeeping, parsing epochs\n",
        "    gm_de440(),                 # mass parameters for major solar system bodies\n",
        "    pck00011(),                 # physical properties of major solar system bodies\n",
        ")\n",
        "\n",
        "mercury = KernelBody(\"mercury barycenter\")\n",
        "venus = KernelBody(\"venus barycenter\")\n",
        "earth = KernelBody(\"earth barycenter\")\n",
        "mars = KernelBody(\"mars barycenter\")\n",
        "jupiter = KernelBody(\"jupiter barycenter\")\n",
        "saturn = KernelBody(\"saturn barycenter\")\n",
        "uranus = KernelBody(\"uranus barycenter\")\n",
        "neptune = KernelBody(\"neptune barycenter\")\n",
        "pluto = KernelBody(\"pluto barycenter\")\n",
        "\n",
        "planets = (mercury, venus, earth, mars, jupiter, saturn, uranus, neptune, pluto)"
      ],
      "execution_count": null,
      "outputs": []
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "All of these orbits are near circular, with the exception of Mercury, and one \n",
        "pesky ex-planet. A perfectly circular orbit will have an eccentricity of zero. \n",
        "\n",
        "!!! note\n",
        "    Mercury's orbit is non-circular due to general relativity!"
      ]
    },
    {
      "cell_type": "code",
      "metadata": {},
      "source": [
        "using Dates\n",
        "using DataFrames\n",
        "using AstrodynamicalCalculations\n",
        "\n",
        "average_eccentricity(planet) =\n",
        "    let epoch = now()\n",
        "        times = epoch-Year(15):Day(1):epoch+Year(15)\n",
        "        ecc(state) = AstrodynamicalCalculations.eccentricity(\n",
        "            state.x,\n",
        "            state.y,\n",
        "            state.z,\n",
        "            state.ẋ,\n",
        "            state.ẏ,\n",
        "            state.ż,\n",
        "            gm(:sun),\n",
        "        )\n",
        "\n",
        "        return sum(time -> ecc(planet(time; wrt = :sun)), times) / length(times)\n",
        "    end\n",
        "\n",
        "name(planet) = bodc2n(SPICEBodies.naifcode(planet))\n",
        "\n",
        "eccentricities = Dict(name(planet) => average_eccentricity(planet) for planet in planets)\n",
        "\n",
        "DataFrame(eccentricities) # hide"
      ],
      "execution_count": null,
      "outputs": []
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "With their (mostly) near-circular orbits established, let's choose an epoch at \n",
        "random and plot their orbital speed, and their _expected_ orbital speed due to \n",
        "the idealized calculation. "
      ]
    },
    {
      "cell_type": "code",
      "metadata": {},
      "source": [
        "using Plots\n",
        "using LinearAlgebra\n",
        "\n",
        "epoch = now()\n",
        "\n",
        "radius(planet) =\n",
        "    let state = planet(epoch)\n",
        "        return sqrt(state.x^2 + state.y^2 + state.z^2)\n",
        "    end\n",
        "\n",
        "actual_speed(planet) =\n",
        "    let state = planet(epoch)\n",
        "        return sqrt(state.ẋ^2 + state.ẏ^2 + state.ż^2)\n",
        "    end\n",
        "\n",
        "ideal_speed(planet) =\n",
        "    let state = planet(epoch)\n",
        "        return AstrodynamicalCalculations.orbital_speed(\n",
        "            state.x,\n",
        "            state.y,\n",
        "            state.z,\n",
        "            state.ẋ,\n",
        "            state.ẏ,\n",
        "            state.ż,\n",
        "            gm(:sun),\n",
        "        )\n",
        "    end\n",
        "\n",
        "artsy = (;\n",
        "    background = :transparent,\n",
        "    grid = false,\n",
        "    markersize = 8,\n",
        "    dpi = 200,\n",
        "    xlabel = \"Radius (KM)\",\n",
        "    ylabel = \"Speed (KM/S)\",\n",
        "    xaxis = :log,\n",
        ")\n",
        "\n",
        "fig = plot(; title = \"Measured Orbital Speed\", artsy...)\n",
        "\n",
        "for planet in planets\n",
        "    scatter!(fig, [radius(planet)], [actual_speed(planet)]; label = name(planet))\n",
        "end\n",
        "\n",
        "plot!(r -> sqrt(gm(:sun) / r); label = \"Theoretical Speed\")"
      ],
      "execution_count": null,
      "outputs": []
    }
  ],
  "metadata": {
    "kernelspec": {
      "name": "julia-1.10",
      "language": "julia",
      "display_name": "Julia 1.10.4",
      "path": "/Users/joey/Library/Jupyter/kernels/julia-1.10"
    }
  },
  "nbformat": 4,
  "nbformat_minor": 4
}